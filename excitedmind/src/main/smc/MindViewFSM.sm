%{
%}

%class MindView
%package excitedmind
%fsmclass MindViewFSM
%import prefuse.visual.NodeItem
%import java.awt.dnd.DragSource

%start MindViewStateMap::Normal

%map MindViewStateMap
%%

Normal
Exit
    {
        stopCursorTimer();
    }
Entry
    {
        mouse_control_set_enabled(true);
        renderTree();
        setCursor(null);
    }
{
    itemEntered(nodeItem: NodeItem)
        nil
    {
        startCursorTimer(nodeItem);
    }

    itemExited(nodeItem: NodeItem)
        nil
    {
        stopCursorTimer();
    }

    cursorTimeout(nodeItem: NodeItem)
        Normal
    {
        m_mindTreeController.setCursorNode(ctxt.m_mindTreeController.toSource(nodeItem));
    }

    cursorLeft
        Normal
    {
        m_mindTreeController.moveCursorLeft();
    }

    cursorRight
        Normal
    {
        m_mindTreeController.moveCursorRight();
    }

    cursorUp
        Normal
    {
        m_mindTreeController.moveCursorUp();
    }
    cursorDown
        Normal
    {
        m_mindTreeController.moveCursorDown();
    }

    itemPressed(nodeItem: NodeItem)
        Normal
        {
            m_mindTreeController.setCursorNode(ctxt.m_mindTreeController.toSource(nodeItem));
        }

    itemClicked()
        Normal
        {
            m_undoManager.addEdit(ctxt.m_mindTreeController.toggleFoldCursorUndoable());
        }

    itemDragged(nodeItem: NodeItem)
        Dragging
        {
        }

    remove
        [ ctxt.canRemove() ]
        Normal
        {
            m_undoManager.addEdit(ctxt.m_mindTreeController.removeCursorUndoable());
        }


    undo
        [ ctxt.m_undoManager.canUndo() ]
        Normal
        {
            m_undoManager.undo();
        }

    redo
        [ ctxt.m_undoManager.canRedo()]
        Normal
        {
            m_undoManager.redo();
        }

    startEditing
        Editing
        {
            startEditing();
        }

    startInserting(asChild: boolean)
        [ ctxt.canStartInserting(asChild) ]
        Inserting
        {
            startInserting(asChild);
        }

    Default
        nil
    {

    }
}

Editing
Entry
    {
        mouse_control_set_enabled(false);
    }
{
    ok
        Normal
        {
            stopEditing(true);
        }
    cancel
        Normal
        {
            stopEditing(false);
        }
    Default
        nil
    {

    }
}

Inserting
Entry
    {
        mouse_control_set_enabled(false);
    }
{
    ok(fromPrompter: boolean)
        Normal
        {
            stopInserting(true, fromPrompter);
        }
    cancel
        Normal
        {
            stopInserting(false, false);
        }
    Default
        nil
    {

    }
}

Dragging
{
    itemDropped(nodeItem: NodeItem, add_ctrl: boolean)
        [add_ctrl == true]
        Normal
        {
            m_undoManager.addEdit(ctxt.m_mindTreeController.addReferenceUndoable(ctxt.m_mindTreeController.toSource(nodeItem)));
        }

    itemDropped(nodeItem: NodeItem, add_ctrl: boolean)
        [add_ctrl == false]
        Normal
        {
            m_undoManager.addEdit(ctxt.m_mindTreeController.resetParentUndoable(ctxt.m_mindTreeController.toSource(nodeItem)));
        }

    cancel
        Normal
        {

        }
    Default
        nil
    {

    }
}

%%
